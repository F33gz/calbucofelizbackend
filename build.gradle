plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.5'
	id 'io.spring.dependency-management' version '1.1.7'
	id "org.sonarqube" version "4.4.1.3373"
	id 'jacoco' // Added JaCoCo plugin
}

group = 'cl.metspherical'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
	implementation 'io.jsonwebtoken:jjwt-impl:0.12.3'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.12.3'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'com.google.cloud:google-cloud-vision:3.36.0'
	implementation group: 'com.cloudinary', name: 'cloudinary-http44', version: '1.39.0'
	implementation 'com.twelvemonkeys.imageio:imageio-core:3.8.1'
	implementation 'com.twelvemonkeys.imageio:imageio-jpeg:3.8.1'
	implementation 'com.twelvemonkeys.imageio:imageio-bmp:3.8.1'
	implementation 'com.twelvemonkeys.imageio:imageio-tiff:3.8.1'
	implementation 'com.twelvemonkeys.imageio:imageio-webp:3.8.1'
	implementation 'org.springframework.security:spring-security-messaging'
	implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'
	implementation 'org.apache.commons:commons-lang3:3.14.0'
	implementation 'me.paulschwarz:spring-dotenv:4.0.0'
	implementation 'com.twilio.sdk:twilio:10.6.3'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
	// Ensure JaCoCo agent is attached to the test JVM
	jacoco {
		enabled = true
		destinationFile = layout.buildDirectory.file('jacoco/test.exec').get().asFile
		classDumpDir = layout.buildDirectory.dir('jacoco/classpathdumps').get().asFile
	}
}

jacocoTestReport {
    // Depends on the test task to ensure execution data is available
    dependsOn tasks.named('test')

    reports {
        xml.required = true // For SonarQube and other tools
        csv.required = false // Typically not needed if XML is generated
        html.required = true // For human-readable reports
    }

    // Configure source and class directories for the report
    sourceDirectories.setFrom(files(sourceSets.main.allSource.srcDirs))
    classDirectories.setFrom(files(sourceSets.main.output))
    executionData.setFrom(files(layout.buildDirectory.file('jacoco/test.exec')))
}

// Optional: Make build task depend on jacocoTestReport to always generate reports
// tasks.named('build') {
//    dependsOn jacocoTestReport
// }
